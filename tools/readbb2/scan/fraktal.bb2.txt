; ascii translation of /Users/simon/Google Drive/amiga/w/disks/bums/bum7/disk1/code/paule/Fraktal.bb2,/Users/simon/Google Drive/amiga/w/lha/bbold/disks/bums/bum7/disk1/code/paule/Fraktal.bb2


;* * * * * * * * * * * * * * * * * * * * * * * *
;*                                             *
;*   Erzeugung von Dreieckfractalen            *
;*                                             *
;*---------------------------------------------*
;*  Demo von: Michael Paulwitz / Berlin        *
;*            im Februar 1994 (PD)             *
;*                                             *
;* * * * * * * * * * * * * * * * * * * * * * * *


WBStartup : NoCli

;Variable definieren

DEFTYPE .f

NEWTYPE .tri
   x0.w:y0:x1:y1:x2:y2
End NEWTYPE


Dim xp.l(6)
Dim yp.l(6)

hoehe = 200-128
re=20
sch=1
g=4.5

gefuellt = 1
allzeich = 1
Durchgang = 4
MaxLen path$=192
MaxLen name$=192

;Gadgets definieren
TextGadget 1,268,23,0,1," 1 | 2 | 3 | 4 | 5 | 6 | 7 "
SetGadgetStatus 1,1,4
TextGadget 1,164,39,0,2,"Nein| Ja " ;siehe USER Magazin 5
TextGadget 1,220,55,0,3,"Nein| Ja "
StringGadget 1,196,71,0,4,4,32
StringGadget 1,244,87,0,5,4,32
StringGadget 1,228,103,0,6,4,32
StringGadget 1,196,119,0,7,4,32


Screen 0,12,"Landschaftsgenerator by MP."
MenuTitle 0,0,"Projekt"
MenuItem  0,0,0,0,"Bild sichern"
MenuItem  0,0,0,1,"Info"
MenuItem  0,0,0,2,"Ende"
MenuTitle 0,1,"Zusatz"
MenuItem  0,0,1,0,"Berechnen"
MenuItem  0,0,1,1,"Endzeichnung"
MenuItem  0,0,1,2,"Konfiguration"

If NTSC             ; Wenn NTSC dann weniger Hhe oder so
  hoch = 200
Else
  hoch = 256
End If

Window 0,0,0,640,hoch,$1900," Landschaftsgenerator",1,2
SetMenu 0
Use Window 0
ScreensBitMap 0,0
*mywindow = Peek.l(Addr Window(0))

Gosub Farbeanders

Repeat
  a.l=WaitEvent
  If a=256  ;menupick?
    If MenuHit=0 AND ItemHit=0
       name$ = "Bild.iff"
       p$ = FileRequest$("Bild sichern",path$,name$)
       If p$ <> "" Then SaveScreen 0,p$
    End If
    If MenuHit=0 AND ItemHit=1 Then Gosub Info
    If MenuHit=0 AND ItemHit=2
       t$ = "Wirklich beenden ?"
       o$ = "Ja|Nein"
       es.EasyStruct\es_StructSize = 20,0,0,&t$,&o$
       ende.b = EasyRequestArgs_(*mywindow,&es,0,0)
       If ende.b = 1 Then End
    End If
    If MenuHit=1 AND ItemHit=0
       Gosub Anfang
       Gosub Beendet
    End If
    If MenuHit=1 AND ItemHit=1
       dummy = gefuellt
       gefuellt = 2
       Gosub Zeichnen
       gefuellt = dummy
       Gosub Beendet
    End If
    If MenuHit=1 AND ItemHit=2 Then Gosub Konfig
  EndIf
Forever

.Anfang:

Dim p(128,128) ;Klaut immer Speicherplatz
d=0.5
w=128
e=1
meer=0
p(20,100)=40
l1=1
l2=0
l3=1
laenge=Sqr(l1*l1+l2*l2+l3*l3)
l1=l1/laenge
l2=l2/laenge
l3=l3/laenge

For m=1 To Durchgang
  jojo.w = Int (m)
  Gosub Doppler
  If allzeich = 2 Then Gosub Zeichnen
  If Joyb(0) =  3 Then Pop For : Return
  w=w/2
Next m
  Gosub Zeichnen
Return

.Zeichnen:
WCls
ss=g*w
hh=Sqr(.75)*ss
v3=ss*hh
vv3=v3*v3

For j=0 To 127 Step w
a=j/2
b=a+w
c=(j+w)/2
dd=c+w
ya=(j+w)*sch+hoehe
yb=j*sch+hoehe
For i=0 To 127-j-w Step w
p1=p(i,j+w)/10
If p1<=meer Then p1=meer
p2=p(i+w,j)/10
If p2<=meer Then p2=meer
p3=p(i+w,j+w)/10
If p3<=meer Then p3=meer
v1=-hh*(p3-p1)
v2=ss*((p1+p3)/2-p2)
laenge=Sqr(v1*v1+v2*v2+vv3)
If laenge = 0 Then laenge = 1
co=(l1*v1+l2*v2+l3*v3)/laenge
If co<0 Then co=0
farb=Int(co*9+6)
;If farb < 6 Then farbe = 6
If p3<=meer
 If p1<=meer
  If p2<=meer
    farb=5
  EndIf
 EndIf
EndIf
dreieck.tri\x0=(i+c)*g+re,ya-p1,(i+b)*g+re,yb-p2,(i+dd)*g+re,ya-p3
If gefuellt = 1
  Poly 3,dreieck,farb
Else
  Polyf 3,dreieck,farb
End If
If Joyb(0) = 3 Then Pop For : Pop For : Goto Weg
Next

For i=0 To 127-j Step w
p0=p(i,j)/10
If p0<=meer Then p0=meer
p1=p(i,j+w)/10
If p1<=meer Then p1=meer
p2=p(i+w,j)/10
If p2<=meer Then p2=meer
v1=-hh*(p2-p0)
v2=ss*((p0+p2)/2-p1)
laenge=Sqr(v1*v1+v2*v2+vv3)
co=(l1*v1+l2*v2+l3*v3)/laenge
If co<0 Then co=0
farb=Int(co*9+6)
If p0<=meer
 If p1<=meer
  If p2<=meer
   farb=5
  EndIf
 EndIf
EndIf
dreieck.tri\x0=(i+a)*g+re,yb-p0,(i+b)*g+re,yb-p2,(i+c)*g+re,ya-p1
If gefuellt = 1
  Poly 3,dreieck,farb
Else
  Polyf 3,dreieck,farb
End If
If Joyb(0)=3 Then Pop For : Pop For : Goto Weg
Next
Next
.Weg
Return

.Doppler:
br=e*w*10
w2=w/2

Window 1,220,100,200,40,0,"       Berechnung ",0,1
Use Window 1
WLocate 26,8
Print "Berechne Runde: "+Str$(m)
For j=0 To 127 Step w
For i=0 To 127-j Step w
b=(p(i,j)+p(i+w,j))/2
p(i+w2,j)=p(i+w2,j)+b+(Rnd-d)*br
b=(p(j,i)+p(j,i+w))/2
p(j,i+w2)=p(j,i+w2)+b+(Rnd-d)*br
b=(p(128-j-i,i)+p(128-j-i-w,i+w))/2
p(128-j-i-w2,i+w2)=p(128-j-i-w2,i+w2)+b+(Rnd-d)*br
Next
Next
Use Window 0
Free Window 1

Return


.Farbeanders:
 ;REM 0-hell
 ;REM 5-wasser
 ;REM 6-15 grau
  RGB 5,0,10,15
  For i=6 To 15
    RGB i,i,i,i
  Next i
Return


.Beendet:
TextGadget 0,26,18,1,1,"Ende mit Mausklick"
Window 1,220,100,200,40,0,"   Berechnung zu Ende",0,1,0
ev.l=WaitEvent
Use Window 0
Free Window 1
Return



.Info:
SizeLimits 32,32,-1,-1
Window 1,148,55,326,131,$1008," Information",1,2
Use Window 1
WLocate 73,9:WJam 0:WColour 1,0
Print "LANDSCHAFTSGENERATOR"
WLocate 104,24
Print "Release 1.0"
WLocate 96,40:WColour 3,0
Print "PUBLIC DOMAIN"
WLocate 59,56:WJam 2:WColour 1,0
Print "Designed an Programmed"
WLocate 70,69
Print "by Michael Paulwitz"
WLocate 62,82
Print "Berlin  SEPTEMBER 1993"
WLocate 14,95 : WJam 4
Print " Zum Abbrechen whrend der Zeichnung "
WLocate 47,106
Print " beide Maustasten drcken "
WJam 0
MouseWait
Use Window 0
Free Window 1
Return

.Konfig:
Borders On:BorderPens 1,2:Borders 4,2
SetString 1,4,Str$(hoehe)
SetString 1,5,Str$(re)
SetString 1,6,Str$(g)
SetString 1,7,Str$(sch)

SizeLimits 32,32,-1,-1
Window 1,148,55,326,150,$1008,"          Konfiguration",1,2,1
Use Window 1
WLocate 20,14:WJam 0:WColour 1,0
Print "Anzahl der Berechnungen (1-7):"
WLocate 20,30
Print "Grafik ausfllen:"
WLocate 20,46
Print "Jeden Durchgang zeichen:"
WLocate 20,62
Print "Hhe des Bildes (72):"
WLocate 20,78
Print "Linke Ecke des Bildes (20):"
WLocate 20,94
Print "Horizontalverzerrt (4.5):"
WLocate 20,110
Print "Vertikalverzerrt (1):"
Repeat
ev.l=WaitEvent
  If ev=64 AND GadgetHit=4
     hoehe = Val (StringText$(1,4))
     If hoehe > 256 Then hoehe=256
     If hoehe < 10 Then hoehe=10
     SetString 1,4,Str$(hoehe)
     Redraw 1,4
  End If
  If ev=64 AND GadgetHit=5
     re = Val (StringText$(1,5))
     If re > 640 Then re = 640
     If re < 1 Then unten=1
     SetString 1,5,Str$(re)
     Redraw 1,5
  End If
  If ev=64 AND GadgetHit=6
     g = Val (StringText$(1,6))
     If g > 20 Then g=20
     If g < 0.1 Then g=0.1
     SetString 1,6,Str$(g)
     Redraw 1,6
  End If
  If ev=64 AND GadgetHit=7
     sch = Val (StringText$(1,7))
     If sch > 10 Then sch=10
     If sch < 0.1 Then sch=0.1
     SetString 1,7,Str$(sch)
     Redraw 1,7
  End If
Until ev=$200
Durchgang = GadgetStatus (1,1)
gefuellt = GadgetStatus(1,2)
allzeich = GadgetStatus(1,3)
Use Window 0
Free Window 1
Return
























